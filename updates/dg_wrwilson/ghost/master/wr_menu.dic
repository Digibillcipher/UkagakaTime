//---------------------------Menus--------------------------
//This .dic file is primarily concerned with the double-click menus for each of your ghosts. If you don't want to have menus for your characters, you'd just have to delete the OnMouseDoubleClick functions near the end of mouse.dic and then ignore this file, as none of it will be called I believe. Menus can be cool though, since you can put stuff in them and create new functions if you know what you're doing. For example, I've added a lot of games and interactions and little linked icons to my Hunter Smoker ghost to keep track of their feelings and such. Much of that is pretty complicated though (and kind of messy, cough), so I won't go into it here, but I covered some of the concepts in the coding page of the walkthrough, and enterprising ghost makers can study the menu.dic file I set up for Hunter/Smoker and see how I put it together.
//Anyway, menu.dic also contains the settings for a config menu that will let your user change their name or pronouns if they want, as well as the talk rate of the ghost, all of which are useful things for your ghost to do. So think hard about whether or not you want to skip this file or not.

//The complexity of this file depends on how customized you want your menus. If you just want to leave them as they are, then all you'll have to do is replace dialogue appropriately. You can always add stuff to their menus later if you get the urge. Otherwise, if you have plans in mind, you'll need to study what's going on in here carefully and pay attention to how things are set up! You can do a lot of unique stuff in this file if you're creative.

//Make sure to read the walkthrough (http://www.ashido.com/ukagaka/) for more details about how to properly format ghost dialogue!
//I will reiterate that you should be editing these in Notepad++, and in particular, you should set the Language to C (or Java I guess) to properly highlight all the text. It will make things A LOT EASIER for you, trust me! It will also help you keep track of your brackets in case you forget some. If you click on a line with a bracket in Notepad++, it should highlight where the other bracket ends, or turn purple if there's no finishing bracket (which you should definitely fix!)
//It will also highlight my commented lines in green, so you'll know where they are. Any line beginning with // is a commented line that the code will not read. It's just for your reference!


//--OpenSakuraMenu
//This is a bit of intro dialogue before the main character opens their menu. You can see below that there are three pieces of separate dialogue listed before it goes to MenuRun. The character will randomly choose one of those pieces of dialogue before opening the rest of their menu. You can customize this with individual greetings for the character, such as "Yes, %(username)?" or "Need something, %(username)?" or things like that. Note that none of the lines end with \e!
//The \b2 tag tells the ghost to use their big balloon for this dialogue. If your menu has a lot of stuff, this'll probably look better than the small one.
//If you don't want multiple responses before opening the menu, you can delete the dialogue and just leave MenuRun.

OpenSakuraMenu
{
	if justpunched == 1
	{
		justpunched = 0
		punchedmenuflag = 1
		"\0\s[44]\w7\s[29]\w6"
		--
		//"\0\b2What's up?"
		"\0\b2Do you... want something...?"
		"\0\b2Need something?"
		"\0\b2Hmm..."
		"\0\b2Yes...?"
		"\0\b2Greetings..."
	}
	elseif takingnotes == 1 || sitting == 1
	{
		noteshour = 0
		takingnotes = 0
		sitting = 0
		punchedmenuflag = 0
		--
		"\0\s[39]\w7\s[38]\w6"
		--
		//"\0%(eyedles)\b2What's up?"
		"\0%(eyedles)\b2Do you want something?"
		"\0%(eyedles)\b2Need something?"
		"\0%(eyedles)\b2Hmm?"
		"\0%(eyedles)\b2Yes?"
	}
	elseif sleeping == 1
	{
		noteshour = 0
		sleeping = 0
		punchedmenuflag = 0
		--
		"\0\s[54]\w6\s[55]\w8\w8\s[39]\w7\s[38]\w6%(eyedles)\b2"
		--
		"Seems that I fell asleep..."
		"\s[9]Ow my neck... "
		"\s[9]Ow my wrist..."
		"I suppose I needed that..."
		"A terrible rest is better than none I suppose..."
		"\s[9]My cheek hurts..."
		"\s[21]\f[sup,true]Hope I didn't drool on my Notebook...\f[sup,false]"
		"\s[46]Ugh, \w8\w8\s[9]I think I got charcoal on my hand..."
		//"What's up?"
		"Need something?"
		"Hmm?"
		"Yes?"
		"Do you want something?"
		"What did you wake me for?"
		
	}
	else
	{
		punchedmenuflag = 0
		//"\0%(eyedles)\b2What's up?"
		"\0%(eyedles)\b2Do you want something?"
		"\0%(eyedles)\b2Need something?"
		"\0%(eyedles)\b2Hmm?"
		"\0%(eyedles)\b2Yes?"
		"\0%(eyedles)\b2Greetings"
	}
	--
	OnCalculateTrust
	MenuRun
	
}

//--MenuRun
//As you just saw above. This actually fills out the rest of the menu. If you're going to be adding or changing functions, this is where you'll be doing your edits.

MenuRun
 {	
 	wd = GetWeekDay
 	ap = GetAP
	StartedCommu = 0
	AlreadyResponded = 0
	floored = 0
	//These four statements basically get the day of the week, whether it's AM or PM, the hour, and the time. It's putting together the information to display the time at the bottom of the balloon. Pay it no mind.
	--
	"%(whwr)\n\n[half]\_q\b2\![no-autopause]" //The \n\n means two new lines as detailed in the walkthrough, and \_q means quick session, meaning all the text from this point on will display instantly instead of a few letters at a time like normal ghost dialogue.
	--
	if lastTalk != "" //What this does is check to see if the ghost has already said a random bit of dialogue on its own, essentially by seeing if the lastTalk thing is NOT empty (!= means "does not equal").
	{
	  "\![*]\q[Can you repeat that?,OnLastTalk]\n\n[half]"
	  //If the ghost has said something, it will then display this menu option. You can replace "Repeat Last Dialogue" with whatever caption you like, such as "What were you saying?" or "What did you say?" or "Could you repeat that?" or something like that.
	  
	  //*********************Advanced User Info*******************************
	  //These text boxes are meant for those who have an interest in expanding or using more complicated functions in their ghost. Also for my own reference in the future since I plan on using this template too, haha. If you have no interest in any of this and just want to keep things as simple as possible, feel free to skip these boxes.
	  
	  //I briefly touched in bootend.dic about \q, but I'll again cover it here. Pay very close attention to how this bit of dialogue is formatted. You'll see it goes \![*]\q[Displayed Name,linkedfunction]. Breaking it down, \![*] puts the balloon's option icon in front of the choice (usually a > or something), \q sets up the choice, Displayed Name which is the text that will display in the ghost's balloon, then linkedfunction, which is what the choice is linked to when the user clicks it. As you go through this file, look at how every menu option is set up, and you will see they all follow this format, give or take a \![*]. If you're going to be adding new options to menus or creating your own functions and such, make sure you follow this format and learn it well. Make sure to read the coding page of the walkthrough.
	  //***********************************************************************
	}
	--
 	"\![*]\q[Talk to me,OnAiTalk]\n"
	"\![*]\q[What's up?,OnAiTalk]\n"
	"\![*]\q[Any knowledge to share?,OnAiTalk]\n"
	"\![*]\q[What's on your mind?,OnAiTalk]\n"
	"\![*]\q[Thinking about anything?,OnAiTalk]\n"
	"\![*]\q[Talk to me,OnAiTalk]\n"
	"\![*]\q[What's up?,OnAiTalk]\n"
	"\![*]\q[Any knowledge to share?,OnAiTalk]\n"
	"\![*]\q[Thinking about anything?,OnAiTalk]\n"
	"\![*]\q[Say something,saysomething]\n"
	--
	if username == "Digi" || username == "Dev"
	{
		"\![*]\q[View Items,viewinv]\n"
		--
		"\![*]\q[DevMenu,devmenu]\n"
	}
	
	//This option will force the ghost to say something from aitalk if the user clicks it. Just replace "Say something" and "Say something 2" with whatever captions you think would be appropriate for such an option. IF you don't want two options for captions, delete one of the lines.
	//Advanced users, you can add multiple captions for any option you add to a menu you make just like this, as long as you separate it from the other options into its own section with two dashes above and below.
	--
	"\![*]\q[Configuration,configmenu]\n"
	--
	if petpet != 0 || punchy != 0
	{
		"\![*]\q[Statistics,statistics]\n"
	}
	--
	if anchormax != 0 || anchorweb != 0 || anchorthr != 0 || anchornoteb != 0 || anchorincident != 0 || anchorcodex != 0
	{
		"\![*]\q[Ask about...,ask]\n"
	}
	--
	if ghostexcount > 0
	{
		//"\![*]\q[Look! Somebody else!,tempname]\n"
		//"\![*]\q[Oh my God! Who is that!?,tempname]\n"
		//"\![*]\q[Holy shit! A person!,tempname]\n"
		
		"\![*]\q["
		--
		"Look! "
		"Oh my God! "
		"Holy shit! "
		"Is that- "
		"Bloody hell! "
		--
		"Somebody else!"
		"Who is that!?"
		"A person!"
		"What is that!?"
		"You're not alone!"
		--
		",notalone]\n"
	}
	--
	/*"\![*]\q[Lottery Coding Fun,lotteryfun]\n"
	--
	"\![*]\q[test,https://www.google.com/]\n"
	--*/
	"\![*]\q[Do Something,dosomething]\n\n[half]/
 	\![*]\q[Nevermind,CANCEL]\n\n[half]%(wd) %(nowhour):%(nowmin) %(ap), %(month)/%(day)/%(year)\e"
	
	//These three lead to the Config Menu, the Function menu, and Cancel. You can leave these alone if you like. All the envelopes at the end (see word.dic) basically display the year, month, day, weekday, and time.
}

//Leave this alone
OnLastTalk 
{
	"%(lastTalk)\e"
}

//This is figuring out what day of the week it is. Leave it alone.
GetWeekDay
{
	if weekday == 0
	{"Sun"}
	elseif weekday == 1
	{"Mon"}
	elseif weekday == 2
	{"Tue"}
	elseif weekday == 3
	{"Wed"}
	elseif weekday == 4
	{"Thu"}
	elseif weekday == 5
	{"Fri"}
	else
	{"Sat"}
}

//This is finding out whether it's AM or PM. Leave it alone.
GetAP
{
 	if hour >= 0 && hour <= 11
 		{"AM"}
 	else
 		{"PM"}
}

nowhour
{
	if hour12 == 0; "1"
	else; "%(hour12)"
}

nowmin
{
	if minute >= 0 && minute <=9; "0%(minute)"
	else; "%(minute)"
}


//If someone selects an option linked to BACKTOMENU, it will go back to MenuRun.
//Advanced users, note that this is formatted as Select.linkedfunction? Remember how it went \q[Displayed Name,linkedfunction] above? When a user clicks an option that doesn't begin with On, they'll be linked to Select.linkedfunction. Pay close attention as we go through and you'll see this in action.

Select.BACKTOMENU
	{
		"\0%(eyedles)\b2"
		--
		"Yes?"
		"Something else then?"
		"Change your mind?"
		--
		MenuRun
	}

//If someone selects configmenu, this will lead to TalkConfig.
Select.configmenu
	{
		TalkConfig
	}
	
Select.notalone
{
	OnStartTalking
}
	
Select.ask
	{
		Askabout
	}
	
Askabout
	{
		"\0%(eyedles)\b2"
		--
		"Alright, what?"
		"About what?"
		"Okay, what?"
		--
		"\n\n%(whwr)"
		--
			if anchormax != 0
			{
				"\![*]\q[Maxwell,askmax]\n"
			}
		--
			if anchorweb != 0
			{
				"\![*]\q[Webber,askweb]\n"
			}
		--
			if anchorthr != 0
			{
				"\![*]\q[The Nightmare Throne,askthr]\n"
			}
		--
			if anchornoteb != 0
			{
				"\![*]\q[The Wretched Notebook,asknoteb]\n"
			}
		--
			if anchorincident != 0
			{
				"\![*]\q[The Incident,askincident]\n"
			}
		--
			if anchorcodex != 0
			{
				"\![*]\q[The Codex Umbra,askcodex]\n"
			}
		--
		"\n\![*]\q[Go back,BACKTOMENU]"
		--
		"\n\![*]\q[Nevermind,CANCEL]\e"
	}
	
Select.askmax
	{
		Askaboutmax
	}
	
Askaboutmax
	{
		"\0\s[51]He offered me a deal, which, \s[31]foolishly, I took. \s[51]I built the machine he'd offered me the knowledge to build, \s[25]little did I know I was building my own door to a world of pain for his amusement.\w8\w8\w8\w8\w8\c\0\s[51]As it turns out, he's been here longer than I could've imagined, trapping others here for entertainment...\w8\s[25] I pity him.\e"
		if anchorincident > 0
		{
			"\0%(eyedles)Maxwell once told me that after The Incident my eyes started reflecting a bit of light. \w8\s[29]%(CHR(34))Like a cat but worse in every way,%(CHR(34)) he said, \w8\s[25]too bad I can't see any better in the dark. \w8\w8\w8\s[88]Now I just try to creep him out if I think he's watching.\s[86]\e"
		}

	}
	
Select.askweb
	{
		Askaboutweb
	}
	
Askaboutweb
	{
		"\0\s[36]Webber's a sweet kid, despite, \s[3]being half spider, \s[35]he's really trying to help me with my arachnophobia.\e"

	}
	
Select.askthr
	{
		Askaboutthr
	}
	
Askaboutthr
	{
		"\0\s[51]My time on the throne wasn't, \s[29]boring, \s[51]to say the least, \s[31]since the only thing I managed to do with it was spy on Maxwell as he did me.\w8\w8\w8\s[35] Although I didn't know he had a sense of humour.\e"

	}
	
Select.asknoteb
	{
		Askaboutnoteb
	}
	
Askaboutnoteb
	{
		"\0\s[38]\w6\s[59]\w7After anThe Incident I decided to upgrade my field notes from just scraps of papyrus to an actual notebook.\w8\w8\w8 Gave me quite the fright when I noticed it followed me around like Maxwell's anCodex Umbra.\w7\s[38]\w6%(eyedles)\e"

	}
	
Select.askincident
	{
		Askaboutincident
	}
	
Askaboutincident
	{
		"\0\s[29]The Incident... \w8Unfortunately those happen often in the Constant. \w8\x%(eyedles)That particular day began as usual, \w8I started by travelling to the pine forest to collect some logs. \w8\s[25]However, \w8when I arrived I discovered the spider dens had spread further than I had anticipated. \w8\x%(eyedles)I thought to myself, \w8\s[8]%(CHR(34))Oh, this shouldn't be too much of an issue. I've dealt with spiders before.%(CHR(34))\w8\w8 \s[61]That's when I heard the hounds... \w8\x\s[25]Before long I was swarmed by a mob of both spiders and hounds, \w8the injuries they inflicted on me where extremely severe. \w8I knew I wouldn't make it very far even after I had defeated them. \w8\x\s[51]The amount of blood-loss clouded my thoughts and I became desperate. \w8I hurriedly constructed a wooden meat effigy as a method to save myself, \w8but by the time I realised I had run out of meat, \w8I was also out of time. \w8\x\b2\s[62]My last ditch effort was to jam whatever chunks of flesh lay around me into its empty arms, \w8be they mine, \w8or my foe's. \w8\s[63]Being reborn via effigy isn't painless sure, \w8but this was like being ripped apart and on fire at the same time. \w8\s[64]Maxwell told me he was watching and had winced when he heard the scream I let out. \w8I don't blame him. \w8\x\s[63]I had to rip what remained of my heat sleeves out of what my lower arms had become. \w8\s[64]I did wonder how I always managed to keep my dignity using those things, \w8but at that moment I wish I didn't. \w8\x\b2\s[62]Afterwards I noticed a few things, \w8like how the pigmen suddenly became aggressive towards me, \w8or how monster meat no longer gave me an upset stomach, \w8\s[63]or how I never kept any scars for long after I gained them. \w8\s[64]I tried everything I could think of to reverse it, \w8but all I gained was an even higher pain tolerance..."
		--
		"\w8\w8\w8\w8\w8\w8\w8\w8\w8\c\0I'm going to sit down...\w8\w8\w8\w8\w8\![raise,OnSitDown]\s[47]\e"
	}
	
Select.askcodex
	{
		Askaboutcodex
	}
	
Askaboutcodex
	{
		"\0\s[29]The Codex Umbra is one seriously creepy book. Whenever Maxwell, \w8succumbed to the elements...\w8 No matter what happened to him it would always reappear on his person eventually. I don't really want to know the extent of its contents.\e"

	}

Select.saysomething
	{
		SaySomething
	}
	
SaySomething
	{
		"\0\s[6]\w8\w8Something.\w8\w8\w8\s[37]\e"
	}
	
Select.devmenu
	{
		DevMenu
	}
	
DevMenu
	{
		"\0\b2\f[name,Belisa plumilla manual plus]\f[height,19]\_q"
		--
		"\![*]\q[Make Sit,makesit]\n"
		--
		"\![*]\q[Make Sleep,makesleep]\n"
		--
		"\![*]\q[Make Write,makewrite]\n"
		--
		"\![*]\q[Reset Font,OnSetFontDefault]\n"
		--
		"\![*]\q[Feign Punch,feignpunch]\n"
		--
		"\![*]\q[Refresh Items,freshtrades]\n"
		--
		"\![*]\q[Apologise,OnApologise]\n"
		--
		"\![*]\q[ResetStats,OnReset]\n"
		--
		"susorry:%(susorry)\n"
		--
		"validapologies:%(validapologies)\n"
		--
		"trust:%(trust)\n"
		--
		"tradescompleted:%(tradescompleted)\n"
		--
		"nopets:%(nopets)\n"
		--
		"numberactions:%(numberactions)\n"
		--
		"Opinion:%(Opinion)\n"
		--
		//"largestfactor:%(largestfactor)\n"
		//_array = (_pokepoke, _punch, _pet, _trade)
		
		if largestfactor == 0
		{
			"largestfactor:poking\n"
		}
		elseif largestfactor == 1
		{
			"largestfactor:punching\n"
		}
		elseif largestfactor == 2
		{
			"largestfactor:petting\n"
		}
		else
		{
			"largestfactor:trading\n"
		}
		
		--
		"\n\![*]\q[Go back,BACKTOMENU]"
		--
		"\n\![*]\q[Nevermind,CANCEL]\e"
	}
	//Sit %(sitting), Sleep %(sleeping), Writting %(takingnotes), HourCheck %(noteshour), PassHour %(passhour), PassMin %(passmin).
Select.makesit
	{
		MakeSit
	}
	
MakeSit
	{
		lastTalk = ""
		takingnotes = 0
		noteshour = 0
		sitting = 1
		--
		"\0\s[47]\e"
	}
	
Select.makewrite
	{
		MakeWrite
	}
	
MakeWrite
	{
		lastTalk = ""
		takingnotes = 1
		--
		"\0\s[38]\w6\s[39]\w7\s[40]\e"
	}

Select.makesleep
	{
		MakeSleep
	}
	
MakeSleep
	{
		lastTalk = ""
		noteshour = 0
		sleeping = 1
		sitting = 0
		--
		"\0\s[48]\e"
	}
	
Select.feignpunch
	{
		FeignPunch
	}
	
FeignPunch
	{
		justpunched = 1
		takingnotes = 0
		noteshour = 0
		--
		"\t\0\s[42]\w8\s[43]\w8"
	}

Select.freshtrades
{
	OnFreshTrades
	DevMenu
}

OnFreshTrades
{
	wilsoninventory = GenerateWilsonInventory //Generate an inventory. You'd want to put this in your OnBoot or whatever, and save it to a global variable so it stays as it is until he closes and you generate a new one
	survivorinventory = GenerateUserInventory
}

OnReset
{
	punchy = 0
	petpet = 0
	pokepoke = 0
	susorry = 0
	validapologies = 0
	tradescompleted = 0
	nopets = 0
}


Select.statistics
	{
		Statistics
	}


Statistics
	{
		"\0\b2\s[91]"
		--
			if punchy == 0
			{
				""
			}
			elseif punchy == 1
			{
				"You punched me once,\s[8] but I'm sure that was some sort of mistake."
			}
			elseif punchy == 2
			{
				"\s[29]You punched me twice!"
			}
			elseif punchy <= 30
			{
				"\s[7]You punched me %(punchy) times!"
			}
			else
			{
				"\s[4]You \w2punch me, \w4a lot..."
			}
		--
			if petpet == 0
			{
				""
			}
			elseif petpet == 1 && punchy > 0
			{
				"\s[29] And you, pet me, once?"
			}
			elseif petpet == 1
			{
				"You, pet me, once?"
			}
			elseif petpet == 2 && punchy > 0
			{
				"\s[29] And you, pet me, twice?"
			}
			elseif petpet == 2
			{
				"You, pet me, twice?"
			}
			elseif petpet > 2 && punchy > 0
			{
				"\s[29] And you've pet me %(petpet) times."
			}
			else
			{
				"You've pet me %(petpet) times."
			}
		--
			if pokepoke == 1
			{
				"\nAlso, you poked me with a needle once."
			}
			elseif pokepoke == 2
			{
				"\nAlso, you've poked me twice with a needle."
			}
			elseif pokepoke > 2
			{
				"\nAlso, you've poked me %(pokepoke) times"
				--
				if lastneedleshell == "The True Needle"
				{
					"\w8, lastly, \w8with a scribbly needle."
				}
				elseif lastneedleshell == "Close-Up View"
				{
					"\w8, lastly, \w8with a huge needle."
				}
				elseif lastneedleshell == "Tiny Pokey Pixels"
				{
					"\w8, lastly, \w8with a blocky needle."
				}
				elseif lastneedleshell == "Syringe"
				{
					"\w8, lastly, \w8with a syringe."
				}
				elseif lastneedleshell == "Eggdle"
				{
					"\w8, lastly, \f[italic,true]\s[77]\w8with an egg???\f[italic,false]"
				}
				else
				{
					" with a needle"
				}
			}
			else //if pokepoke doesn't exist
			{
				""
			}
		--
		"\n\n\w8\w8\w8"
		--
		if largestfactor == 0
		{
			//"largestfactor:poking\n"
			if pokepoke < 10
			{
				"\s[29]What's with the poking anyway?"
			}
			elseif pokepoke < 20
			{
				"\s[29]You seem to poke me a lot..."
			}
			else
			{
				"\s[29]I wish you wouldn't poke me all the time."
			}
		}
		elseif largestfactor == 1
		{
			//"largestfactor:punching\n"
			if punchy < 4
			{
				"\s[29]I hope you don't make it a habit of,\w8\s[89] accidentally?\w8\s[29] punching me in the face."
			}
			elseif punchy < 7
			{
				"\s[7]It's not normal to punch people in the face without good reason, you know."
			}
			elseif punchy < 11
			{
				"\s[89]Why are you doing this? \w8Why are you punching me of all people? There must be a reason."
			}
			elseif punchy < 31
			{
				"\s[64]You use me like a punching bag, I just don't understand why you'd do such a thing..."
			}
			else
			{
				"\s[29]\f[italic,true]\w8I think we both know you're just a fucking cunt.\f[italic,false]."
			}
		}
		elseif largestfactor == 2
		{
			//"largestfactor:petting\n"
			if petpet < 10
			{
				"\s[69]You seem to like my hair?"
			}
			elseif petpet < 20
			{
				"%(embarrassed)You must really like my hair."
			}
			else
			{
				"%(embarrassed)My hair\w8.\w8.\w8. You play with it a lot..."
			}
		}
		else
		{
			//"largestfactor:trading\n"
			if tradescompleted < 10
			{
				"\s[35]Thanks for trading with me."
			}
			elseif tradescompleted < 20
			{
				"\s[37]You know all these supplies you've been trading me have been extremely useful."
			}
			else
			{
				"\s[5]Your generosity knows no bounds, I don't know how to thank you enough!"
			}
		}
		--
		"\n\n[half]\w8%(whwr)"
		--
		"\![*]\q[Okay,CANCEL]\w8\w8\w8\w8\w8\w8\w8\w8\w8\w8\w8\w8%(eyedles)\e"
	}
	
Select.viewinv
	{
		DisplayInventory
	}

//*********************Advanced User Info*******************************
//Some of you may be wondering why some of these Select.options lead to another function. Why not just put the config menu coding under Select.configmenu, instead of linking to TalkConfig?
//Basically any function that's preceded with Select. is limited in its complexity and can only be called within its own .dic file in my experience. Why should you care, you ask? Well, if you're going to link to this function any other way, such as linking it in other .dic files or as a page in a menu, it needs to be a stand-alone function so it can be properly called. I'm pretty sure.
//The basic jist is this; if you're going to reuse whatever it is the option leads to, you should make it its own function. Otherwise, you can leave it in Select.option. As a rule of thumb I'd probably put any complicated stuff in its own function, then use the Select.option to call the full function as you can see above. Simple dialogue responses to a choice can stay as a Select.option though (Like "Do you like going to raves and partying? Yes/No"). Study what stays as a Select.option and what links to another Function as we go through the file.
//***********************************************************************

//--TalkConfig
//This menu allows the user to change their name and other information, as well as put in their birthday and change how often the ghost speaks. You can restructure how this looks if you want, but it's perfectly functional as is. You can of course change the dialogue though if you like.

TalkConfig
{
	"\0%(whwr)\![no-autopause]\b[2]\_q" 
	//This bit of dialogue sets up a large balloon and a quick session, like above, so all the text will appear immediately. The rest is formatted to display the information and options to change it. I wouldn't touch any of it if you don't know what you're doing.
	//I'll briefly explain the font tags I used here. \f[bold,1] turns bold on, \f[bold,0] turns it off, not unlike a <b> tag. \f[sup,1] turns superscript on (making it tiny and above the text) and \f[sup,0] turns it off. I touch on these tags in the walkthrough on SakuraScript.
	--
	"Configuration Options\n\n"
	--
	"\f[bold,1]Your Name:\f[bold,0] %(username) \f[sup,1](\q[Change it?,TEACHNAME])\f[sup,0]\n"
	--
	"\f[bold,1]Your Pronouns:\f[bold,0] %(heshe)/%(himher)/%(hisher) \f[sup,1](\q[Change them?,TEACHPRONOUN])\f[sup,0]\n"
	--
	"\f[bold,1]Your Birthday:\f[bold,0] %(birthdayprint) \f[sup,1](\q[Change it?,TEACHBDAY])\f[sup,0]\n"
	--
	"\f[bold,1]Talk Font:\f[bold,0] %(currentfontprint) \f[sup,1](\q[Change it?,CHANGEFONT])\f[sup,0]\n"
	--
	if talktime == "Never..."
	{
		"\f[bold,1]Talk Rate:\f[bold,0] %(talktime) \f[sup,1](\q[Change it?,talkinter])\f[sup,0]\n\n"
	}
	else
	{
		"\f[bold,1]Talk Rate:\f[bold,0] Every %(talktime) \f[sup,1](\q[Change it?,talkinter])\f[sup,0]\n\n"
	}
	--
	"\![*]\q[Enable / Disable Options ,enablables]\n\n[half]"
	--
	"\![*]\q[Go back,BACKTOMENU]"
	--
	"\n\![*]\q[Nevermind,CANCEL]\e"
}

Select.enablables
{
	Enablables
}

Enablables
{
	"\0%(whwr)\![no-autopause]\![set,autoscroll,disable]\b[2]\_q" 
	--
	"Enable / Disable Options\n\n"
	--		
		{
		"\f[bold,1]Edge Notifications?\f[bold,0] \q[%(edgescreen),edgeon]\n\n[half]"
		}
	--
		{
		"\f[bold,1]Hour Notifications?\f[bold,0] \q[%(hournotify),houron]\n\n[half]"
		}
	--
		{
		"\f[bold,1]Comment on Programs?\f[bold,0] \q[%(programtalk),progon]\n\n[half]"
		}
	--
		{
		"\f[bold,1]Perceive your Mouse?\f[bold,0] \q[%(seemouse),mouseon]\n\n[half]"
		}
	--
		{
		"\f[bold,1]Punch confirm?\f[bold,0] \q[%(punchcheck),punchcheckon]\n\n[half]"
		}
	--
		{
		"\f[bold,1]Make Sound?\f[bold,0] \q[%(soundcheck),soundcheckon]\n\n[half]"
		}
	--
		{
		"\f[bold,1]Enable Uninstall?\f[bold,0] \q[%(deleteshitai),deleteon]\n\n[half]"
		}
	--
	"\n\![*]\q[Go back,configmenu]"
		--
	"\n\![*]\q[Nevermind,CANCEL]\e"
}

//This and the next few set up the pronouns, as you can see by the linkedfunction names. You can replace "\0\s[0]What pronouns do you want?" with your own dialogue if you prefer, but leave the rest of it alone.

Select.TEACHPRONOUN
{
	"\0%(eyedles)What are your pronouns?%(whwr)\w5\n\n[half]\_q/
		\![*]\q[He-His-Him,choicemasc]\n/
		\![*]\q[She-Her-Her,choicefem]\n/	
		\![*]\q[They-Their-Them,choiceneither]\e"
}

Select.choicemasc
{
	presuffix = "masculine"
	himher = "him"
	heshe = "he"
	hisher = "his"
	hesshes = "he's"
	--
	"\0%(eyedles)Okay you're he him and his.\e"
	//Leave the part above the double dashes alone. You can replace the snip of dialogue however with whatever you like.
}

Select.choicefem
{
	presuffix = "feminine"
	himher = "her"
	heshe = "she"
	hisher = "her"
	hesshes = "she's"
	--
	"\0%(eyedles)Okay you're she her and her.\e"
	//as above, you can also replace this dialogue.
}

Select.choiceneither
{
	presuffix = "undefine"
	himher = "them"
	heshe = "they"
	hisher = "their"
	hesshes = "they're"
	--
	"\0%(eyedles)Okay you're they them and their.\e"
	//and this one.
}

//Here are some examples of these envelopes in use.
//"%(heshe) left that there."
//"%(hisher) work is really cool."
//"I'll ask %(himher) later."
//"%(hesshes) really good at it."

OnfixConfig //%(OnfixConfig)
{
	edgescreen = "ON"
	hournotify = "ON"
	programtalk = "OFF"
	deleteshitai = "OFF"
	seemouse = "OFF"
	punchcheck = "ON"
	soundcheck = "ON"
}

Select.edgeon
{
	if edgescreen == "ON"
	{
		edgescreen = "OFF"
	}
	else
	{
		edgescreen = "ON"
	}
	Enablables
}

Select.houron
{
	if hournotify == "ON"
	{
		hournotify = "OFF"
	}
	else
	{
		hournotify = "ON"
	}
	Enablables
}

Select.progon
{
	if programtalk == "ON"
	{
		programtalk = "OFF"
	}
	elseif programtalk == "DISABLED"
	{
		programtalk = "DISABLED"
	}
	else
	{
		programtalk = "ON"
	}
	Enablables
}

Select.mouseon
{
	if seemouse == "ON"
	{
		seemouse = "OFF"
	}
	else
	{
		seemouse = "ON"
	}
	Enablables
}

Select.punchcheckon
{
	if punchcheck == "ON"
	{
		punchcheck = "OFF"
	}
	else
	{
		punchcheck = "ON"
	}
	Enablables
}

Select.soundcheckon
{
	if soundcheck == "ON"
	{
		soundcheck = "OFF"
	}
	else
	{
		soundcheck = "ON"
	}
	Enablables
}

//These two options toggle whether or not you can delete your ghost. Leave them alone.

Select.deleteon
{
	if deleteshitai == "ON"
	{
		deleteshitai = "OFF"
	}
	else
	{
		deleteshitai = "ON"
	}
	Enablables
}

//This next set of functions handles inputting the user's birthday.

Select.TEACHBDAY
	{	
		"\0\s[53]What month were you born? Name or number is fine.\![open,inputbox,OnUserBornMonth,-1]\e"
		
		//You can replace the dialogue up to the \![open,inputbox,OnUserBornMonth,-1] tag, leave that alone. You'll need the input box to get the information.
		
		//*********************Advanced User Info*******************************
		//I also touched on this in bootend.dic and explained it in the walkthrough page on coding, but I'll also go into it here. I'm going to break down the tag there at the end of the sentence so you know what it's doing. Basically, \! means a function is going to happen, open tells it to open something, inputbox tells it to open an input box, and OnUserBornMonth is the function the input box will be linked to, which is just below here. -1 means the input box should not time out. If you want to add input boxes to some new function for whatever reason, you should follow this basic format.
		//On a related note, you can raise any function at the end of any piece of dialogue by using this format - \![raise,InsertFunctionName]. This can be handy for testing things if the function is hard to get to run otherwise.
		//***********************************************************************
	}

//--OnUserBornMonth
//This takes the value that the user put into the input box above and evaluates it. Most of this you won't have to touch, but I'll explain what it's doing anyway. I'll point out where you can change the dialogue.

OnUserBornMonth
{
	_tempmonth = TOSTR(reference0) //this stores what the user put in as a variable called _tempmonth
	_tempmonth = TOLOWER(_tempmonth) //changes it to lowercase
	
	//this is attaching the right value to the input from the user
	if _tempmonth == "january" || _tempmonth == "1" {bornmonth = 1; bornmonthprint = "January"}
	elseif _tempmonth == "february" || _tempmonth == "2" {bornmonth = 2; bornmonthprint = "February"}
	elseif _tempmonth == "march" || _tempmonth == "3" {bornmonth = 3; bornmonthprint = "March"}
	elseif _tempmonth == "april" || _tempmonth == "4" {bornmonth = 4; bornmonthprint = "April"}
	elseif _tempmonth == "may" || _tempmonth == "5" {bornmonth = 5; bornmonthprint = "May"}
	elseif _tempmonth == "june" || _tempmonth == "6" {bornmonth = 6; bornmonthprint = "June"}
	elseif _tempmonth == "july" || _tempmonth == "7" {bornmonth = 7; bornmonthprint = "July"}
	elseif _tempmonth == "august" || _tempmonth == "8" {bornmonth = 8; bornmonthprint = "August"}
	elseif _tempmonth == "september" || _tempmonth == "9" {bornmonth = 9; bornmonthprint = "September"}
	elseif _tempmonth == "october" || _tempmonth == "10" {bornmonth = 10; bornmonthprint = "October"}
	elseif _tempmonth == "november" || _tempmonth == "11" {bornmonth = 11; bornmonthprint = "November"}
	elseif _tempmonth == "december" || _tempmonth == "12" {bornmonth = 12; bornmonthprint = "December"}
	
	else //if the month isn't a valid month, like 32 or something.
	{
			"\0\s[51]That doesn't sound right.\s[31] Try again?"
			--
			"\![open,inputbox,OnUserBornMonth,-1]\![set,balloontimeout,-1]\e"
			
			//You can replace this dialogue in the first line. You need to leave the tag for the input box so they can put in a correct month. That last tag there for ![set,balloontimeout,-1] makes it so the input box won't time out if the user takes too long.
	}
	--
	//If it's a valid month
	
	"\0\s[53]So you were born in %(bornmonthprint).\n\w8What day was it?"
	--
	"\![open,inputbox,OnUserBornDay,-1]\![set,balloontimeout,-1]\e"
	
	//Here you can see the use of a bornmonthprint envelope to show the user what month they put in. Notice that it opens an inputbox at the end the same way the one above did. You can replace the dialogue in the first line just as with above.
}

//This adds an appropriate suffix to the number for the day. No need to mess with this.

Printday
{
	if bornday == 1 || bornday == 21 || bornday == 31
	{	"%(bornday)st"	}
	elseif bornday == 2 || bornday == 22
	{	"%(bornday)nd"	}
	elseif bornday == 3 || bornday == 23
	{	"%(bornday)rd"	}
	else
	{	"%(bornday)th"	}
}	

//--OnUserBornDay
//This is much like the above function OnUserBornMonth. It'll take the value the user put in for the day and analyze it. Again, there'll be comments explaining how it works, but all you really need to do is change the dialogue appropriately. I'll point out where you can add new dates to check for.

OnUserBornDay
{
	_tempday = reference0 //storing the input in _tempday
	_tempday = TOLOWER(_tempday) //changes it to lowercase
	_tempday = REPLACE(_tempday,"st","") //if the user types something like 2nd, 3rd, etc, this will make it just the number. It'll just add the suffix back later on the one that actually shows.
	_tempday = REPLACE(_tempday,"nd","")
	_tempday = REPLACE(_tempday,"rd","")
	_tempday = REPLACE(_tempday,"th","")
	_tempday = TOINT(_tempday) //Converts it to an integer
	
	if ((bornmonth == 1 || bornmonth == 3 || bornmonth == 5 || bornmonth == 7 || bornmonth == 8 || bornmonth == 10 || bornmonth == 12) && _tempday >= 1 && _tempday <= 31) || (bornmonth == 2 && _tempday >= 1 && _tempday <= 29) || ((bornmonth == 4 || bornmonth == 6 || bornmonth == 9 || bornmonth == 11) && _tempday >= 1 && _tempday <= 30) //This ugly function checks if the day falls into a valid range. If it does, we set some variables and continue down.
	{
		bornday = _tempday
		borndayprint = Printday
		userbirthday = "%(bornmonth)month %(bornday)day"
		birthdayprint = "%(bornmonthprint) %(borndayprint)"
		
		"%(birthchecked)"
		//Mostly defining envelopes and values in here. No need to touch any of this! Eagle-eyed readers will notice the new envelope birthchecked on its own line like a tiny king. We'll get to that momentarily, but pay it no mind for now.
	}
	else //If the day isn't valid, like the 10046th or something, this will open the input box again and have a \e so the dialogue below doesn't play. You can replace the dialogue in the first line here.
	{
		"\0\s[23]That doesn't sound right.\s[31] Try again?"
		--
		"\![open,inputbox,OnUserBornDay,-1]\e"
	}	
}

//--birthchecked
//This is the new envelope you saw me point out above. As you can see here, you can actually call dialogue in a function with an envelope, even when it has if and else statements like here! Which can be pretty handy. Again, more on envelopes in word.dic. Creative ghostmakers can find handy ways to exploit this, I'm sure.

birthchecked
{
	if userbirthday == dayslot //if today is the user's birthday
		{
			"\0\s[5]Happy birthday, %(username)!\e"
			//replace this dialogue with whatever you like
		}
	elseif userbirthday == "12month 25day"
		{
			"\0\s[5]Happy holidays and Happy birthday, %(username)!\e"
			//As you can see, you can set up an elseif statement to check if the user's birthday is any day you want! More on elseif statements in bootend.dic. In this case, there's specialized dialogue if the user was born on Christmas. You can replace this with whatever you like, or change the date you're checking as well, like to one of your character's birthdays if you want. You can also delete this elseif if you don't care.
		}
	else //if the birthday is not listed above.
		{
			"\0%(eyedles)Alright your birthday is %(bornmonthprint) %(borndayprint).\e"
			//note the use of envelopes to display the newly set birthday properly. You can replace this dialogue as well, and you can use %(bornmonthprint) and %(borndayprint) to display the month and day in the new dialogue, if you want.
		}
}

//--Talk Rate
//This is when the user chooses to adjust the talk rate in the config menu. The talk rate is how often the ghost will read some dialogue from aitalk.dic.
//note the new envelope here for %(talktime). You'll see it again in a second. You can replace the dialogue above with whatever you like, just make sure it ends with ./ and not \e.
Select.talkinter
 	{
		if talktime == "Never..."
		{
			"\0%(eyedles)\b2How often should I say something?\n\w8\w8\s[29]I'm not talking right now..."
		}
		else
		{
			"\0%(eyedles)\b2How often should I say something?\n\w8Currently it's every %(talktime)."
		}
		--
 		"\0\n\n[half]%(whwr)"
		--
 		"\![*]\q[Every 30 seconds,TALKMAX]\n"
		--
		"\![*]\q[Every minute,TALKHIGH]\n"
		--
 		"\![*]\q[Every 3 minutes,TALKNORMAL]\n" //HEY MAKE THESE VARY SLIGHTLY WITH RAND
		--
		"\![*]\q[Every 5 minutes,TALKPOOR]\n"
		--
 		"\![*]\q[Don't speak,TALKNONE]\n\n[half]"
		--
		"\![*]\q[Don't change,TalksTill]\e"
	}

//If the user doesn't want to change the rate. You can change all of the dialogue in any of these options if you want to reflect the choice the user made. You TECHNICALLY could also change the value in talktime to reflect some other way of measuring time but I wouldn't recommend it, it'd just be confusing I'm sure.
Select.TalksTill
	{
		"\0%(eyedles)Okay then.\e"
	}

//If the user chooses every 30 seconds.
Select.TALKMAX
	{
 		talktime = "30 seconds" //this sets up the envelope properly
		aitalkinterval = 30 //this changes the actual rate
		
 		"\0\s[5]You must really like what I have to say!\e"
	}

//If the user chooses every minute.
Select.TALKHIGH
	{
 		talktime = "minute"
		aitalkinterval = 60
		
 		"\0\s[53]Okay I'll say something every minute. \w8\w8%(eyedles)\e"
	}

//If the user chooses every three minutes.
Select.TALKNORMAL
	{
 		talktime = "3 minutes"
		aitalkinterval = 180
		
 		"\0\s[53]Okay I'll say something every three minutes. \w8\w8%(eyedles)\e"
	}

//If the user chooses every five minutes. This is the default rate the ghost will start up in, as seen in bootend.dic.
Select.TALKPOOR
	{
 		talktime = "5 minutes"
		aitalkinterval = 300
		
 		"\0\s[53]Okay I'll say something every five minutes. \w8\w8%(eyedles)\e"
	}

//If the user chooses to not have them speak. This is one case where you MIGHT want to make talktime equal something else, if you have a snappier way of saying the ghost won't talk. Make sure it flows well in your menus though, since it will be displayed in your config menu.
Select.TALKNONE
	{
 		talktime = "Never..."
		aitalkinterval = 300
		bequiet = 1
		
		"\0\s[28]Okay fine.\e"
	}

	
//This is when the user chooses to change their name. It's linked to the functions outlined in nameteach.dic. Nothing to touch here.
Select.TEACHNAME
	{
		OnTeachName	
	}
	
Select.CHANGEFONT
{
	"\0\s[89]You want to change what typeface I'm speaking in? \w8\w8Alright...\n\n[half]\b2\_q"
	--
	OnChangeFont
}

BackToFonts
{
	"\0\b2\s[89]How does \w4this \w4look..?\n\n[half]\_q"
	"\0\b2\s[89]Does this \w4look good..?\n\n[half]\_q"
	"\0\b2\s[89]It changed \w4I think..?\n\n[half]\_q"
	"\0\b2\s[89]How's \w4this..?\n\n[half]\_q"
	--
	OnChangeFont
}

OnBackToFontsTwo
{
	"\![close,inputbox,__SYSTEM_ALL_INPUT__]\0\b2\s[89]You want to change something else?\n\n[half]\_q"
	--
	OnChangeFont
}

BackToFontsWriting
{
	"\0\b2\s[89]This feels \w4familiar..?\n\n[half]\_q"
	--
	OnChangeFont
}

OnChangeFont
{
	"%(whwr)"
	--
	"\![*]\q[Belisa Plumilla Manual Plus,DEFAULTFONT]\n"
	--
	"\![*]\q[Wilson's Handwriting,HANDWRITING]\n"
	--
	"\![*]\q[Times New Roman,NEWROMAN]\n"
	--
	"\![*]\q[Other Font,OTHERFONT]\n\n[half]"
	--
	"\![*]\q[Change Font Size,FONTSIZE]\n\n[half]"
	--
	"\![*]\q[Go back,configmenu]"
	--
	"\n\![*]\q[Nevermind,CANCEL]\e"
}

Select.DEFAULTFONT
{
	currentfont = "Belisa plumilla manual plus"
	currentfontprint = "Belisa Plumilla Manual Plus"
	kerocurrentfont = "Belisa plumilla manual plus"
	if fontsize == "Default"
	{
		currentfontsize = 19
		kerocurrentfontsize = 19
	}
	elseif fontsize = kerofontsize
	{
		currentfontsize = TOINT(fontsize)
		kerocurrentfontsize = TOINT(fontsize)
	}
	else
	{
		currentfontsize = TOINT(fontsize)
		kerocurrentfontsize = TOINT(kerofontsize)
	}
	//"\f[name,%(currentfont)]\f[height,%(currentfontsize)]"
	BackToFonts
}

Select.NEWROMAN
{
	currentfont = "Times New Roman"
	currentfontprint = "Times New Roman"
	kerocurrentfont = "Times New Roman"
	if fontsize == "Default"
	{
		currentfontsize = 17
		kerocurrentfontsize = 17
	}
	elseif fontsize = kerofontsize
	{
		currentfontsize = TOINT(fontsize)
		kerocurrentfontsize = TOINT(fontsize)
	}
	else
	{
		currentfontsize = TOINT(fontsize)
		kerocurrentfontsize = TOINT(kerofontsize)
	}
	BackToFonts
}

Select.HANDWRITING
{
	currentfont = "WilsonPH"
	currentfontprint = "Wilson's Handwriting"
	kerocurrentfont = "Belisa plumilla manual plus"
	if fontsize == "Default"
	{
		currentfontsize = 20
		kerocurrentfontsize = 19
	}
	elseif fontsize = kerofontsize
	{
		currentfontsize = TOINT(fontsize)
		kerocurrentfontsize = TOINT(fontsize)
	}
	else
	{
		currentfontsize = TOINT(fontsize)
		kerocurrentfontsize = TOINT(kerofontsize)
	}
	BackToFontsWriting
}

Select.FONTSIZE
{
	"\0\s[89]You want to change the typesize? \w8\w8Alright... \w8\w8Tell me the value or type %(CHR(34))Default%(CHR(34))\*"
	--
	FontSizePartTwo
}

FontSizePartTwo
{
	"\![open,inputbox,OnFontSizePartThree,-1]"
	--
	"\w8\w8\w8\s[29] Please don't tell me the in Longhand, or Roman numerals...\n\n[half]%(whwr)"
	--
	"\![*]\q[Go back,OnBackToFontsTwo]"
	--
	"\n\![*]\q[Nevermind,CANCEL]"
}

OnFontSizePartThree
{
	_answer = reference0
	_answer = TOLOWER(_answer)
	_answer = REPLACE(_answer," ","") //removes spaces
	_answer = REPLACE(_answer,".","") //removes periods
	_answer = REPLACE(_answer,"-","") //removes dashes
	
	if "default" _in_ _answer || "normal" _in_ _answer || "regular" _in_ _answer
	{
		fontsize = "Default"
		kerofontsize = "Default"
		if currentfont == "WilsonPH"
		{
			currentfontsize = 20
			kerocurrentfontsize = 19
		}
		elseif currentfont == "Times New Roman"
		{
			currentfontsize = 17
			kerocurrentfontsize = 17
		}
		else
		{
			currentfontsize = 19
			kerocurrentfontsize = 19
		}
		BackToFonts
	}
	else
	{
		_answer = TOINT(_answer)
		if _answer == 0
		{
			"\0\s[89]That doesn't sound right, try again?\w8\w8"
			--
			FontSizePartTwo
		}
		else
		{
			currentfontsize = _answer
			kerocurrentfontsize = _answer
			kerofontsize = _answer
			fontsize = _answer
			BackToFonts
		}
	}
}

Select.OTHERFONT
{
	"\0\s[89]What typeface do you want..?\n\w8\w8Try to be exact.\n\n[half]%(whwr)\*"
	--
	"\![open,inputbox,OnOtherFont,-1]"
	--
	"\![*]\q[Go back,OnBackToFontsTwo]"
	--
	"\n\![*]\q[Nevermind,CANCEL]"
}

OnOtherFont
{
	_answer = reference0
	currentfont = _answer
	currentfontprint = _answer
	kerocurrentfont = _answer
	--
	"\0\b2\s[89]Did it change..?\n\n[half]\_q"
	"\0\b2\s[89]Did it work..?\n\n[half]\_q"
	"\0\b2\s[89]Is this it..?\n\n[half]\_q"
	--
	OnChangeFont
}

whwr
{
	if currentfont == "WilsonPH"
	{
		"\f[name,%(kerocurrentfont)]\f[height,%(kerocurrentfontsize)]"
	}
}

OnSetFontDefault
{
	currentfont = "Belisa plumilla manual plus"
	currentfontprint = "Belisa Plumilla Manual Plus"
	currentfontsize = 19
	fontsize = "Default"
	kerocurrentfont = "Belisa plumilla manual plus"
	kerocurrentfontsize = 19
	kerofontsize = "Default"
}








	
//If the user selects cancel in a menu. Replace as you like, or just leave them blank.
Select.CANCEL
	{
		"\![close,inputbox,__SYSTEM_ALL_INPUT__]\0%(eyedles)Okay then.\e"
		"\![close,inputbox,__SYSTEM_ALL_INPUT__]\0%(eyedles)Alright.\e"
		--
		OnCalculateTrust
 	}

	
//Going back to the MenuRun function we finished above, this is if someone selects the Functions option, linked to dosomething. As you can see, it ends up as Select.dosomething.

Select.dosomething
{
	"\0%(eyedles)\b2"
	--
 	"Alright what?"
 	"Okay what?"
	"Yes what?"
	--
	"\n\n%(whwr)"
	//I mentioned this above in MenuRun, but you can have a set of multiple different responses that may randomly appear when the menu is opened if you offset them from the rest with the -- dashes below this note. Try opening this menu yourself with the template to see how it works. The character will randomly choose one line as the intro. 
	//If you don't want to deal with this, just delete the extra lines and leave one. Otherwise, replace the dialogue above as you like, but note that they don't end with \e here. You can leave the rest of this below alone.
	--
	"\![*]\q[Please sit down,canyousit]\n"
	--
	if seemouse == "ON"
	{
		"\![*]\q[Hey watch this,watchthis]\n"
	}
	--
	if programtalk == "ON"
	{
		if "Windows" _in_ os_name
		{
			"\![*]\q[See anything interesting?,seeanything]\n"
		}
		else
		{
			programtalk = "DISABLED"
		}
	}
	--
 	"\![*]\q[Open the Calendar,CALENDAR]\n"
	--
	"\![*]\q[Check my Electronic Mail,MailCheck]\n"
	--
	"\![*]\q[Open my SSP Preferences,sspconfig]\n"
	--
	"\![*]\q[Empty my Recycle Bin,ChoiceGarbagecan]\n"
	--
	"\![*]\q[Check my System Information,ChoiceSystemInformation]\n\n[half]"
	--
 	"\n\![*]\q[Go back,BACKTOMENU]"
	--
	"\n\![*]\q[Nevermind,CANCEL]\e"
}

Select.canyousit
	{
		CanYouSit
	}
	
CanYouSit
	{
		lastTalk = ""
		takingnotes = 0
		noteshour = 0
		sitting = 1
		--
		"\0Um, alright...\s[38]\w6\s[39]\w7\s[47]\e"
	}
Select.watchthis
{
	WatchThis
}
	
WatchThis
{
    _display = ""
    for _i = 0; _i < 5; _i++
    {
        _display += "\0\![embed,OnLookMouse]\w8\w8\w8\w8"
    }
    _display
	--
	"%(eyedles)"
	--
	"Huh, \w4interesting..."
	"Uh, \w4interesting..."
	"Um, \w4interesting..."
	"Um, \w4I don't get it..."
	"Uh, \w4sure."
	"I'm afraid I don't quite understand what's happening there."
	"I'm gonna be honest, \w4I don't know what you did."
	"You must've done something, \w8yes..."
	"Okay, \w4yep..."
	"Your pointer, \w4thing, \w8yeah..."
	--
	"\e"
}

Select.seeanything
{
	SeeAnything
}

SeeAnything
{
	"%(OnProgramSearch)"
}

//When the user clicks Open Calender. This will open the calender function of SSP, an in-built feature that you don't have to worry about in the least.
Select.CALENDAR
{
	"\0\s[8]Loose track of time? \![open,calendar]\e"
	"\0\s[53]Loose track of time? \w8\s[8]I do too.\w8\![open,calendar]\e"
	
	//You can replace the dialogue here up to the \!.
	//Advanced users, can you break it down yet? It's basically just using \! to open calender.exe or dll or whatever SSP uses to make it happen.
}

//When the user chooses SSP preferences. This will open the same window that shows up if you right click your ghost and go to Utilities - Preferences.
Select.sspconfig
{
	"\0%(eyedles)Alright I'll open the SSP config menu. \![open,configurationdialog]\e"
	
	//Again, you can replace up to the \! tag.
}

//If the user chooses to empty the recycle bin.
Select.ChoiceGarbagecan
{
	_gomicheck = FUNCTIONEX("gomi.dll", "-n")	//Don't touch this line. for those curious, it's referencing a SAORI, which are optional user-made plug-in dlls you can download to enable different functions on your ghost. gomi.dll is one included with this template. All SAORI are in Japanese (maybe Korean or Chinese as well idk) and many have disappeared by now, so don't concern yourself very much with SAORI. They're for skilled developers, mostly.
	
	if _gomicheck == 0 //If the recycle bin is empty.
	{
		"\0%(eyedles)Okay, \s[8]maybe I'll find something useful. \w8\w8\s[-1]\w8\w8\w8\w8\w8%(eyedles)It was empty.\e"
		
		//This isn't the usual flat template dialogue, mostly because I wanted to show that this is a good place to use s[-1], which I covered in the walkthrough file. You can have your ghost disappear momentarily like they're going to check your recycle bin, then come back. \c clears the ghost's balloon of text. Replace dialogue as you desire.
	}
	elseif _gomicheck >= 1 //If the recycle bin is full.
	{
		"\0%(eyedles)Okay, \s[8]maybe I'll find something useful. \w8\w8\s[-1]\w8\w8\w8\w8\w8\![raise,OnGarbagecanEmpty]\w9\w9\w9%(eyedles)\c\0Done.\e"
		
		"\0%(eyedles)Okay, \s[8]maybe I'll find something useful. \w8\w8\s[-1]\w8\w8\w8\w8\w8\![raise,OnGarbagecanEmpty]\w9\w9\w9%(eyedles)\c\0Done, didn't find anything.\e"
		
		//Somewhat similar to the above, but notice the \![raise],OnGarbagecanEmpty] tag in there. That's what actually empties the recycle bin, so make sure you don't delete that tag. Otherwise, replace the dialogue as you want.
		//As you can see, you can have multiple pieces of dialogue in here, just like you can for many other functions scattered through the .dic files. If you only want the one, just delete the others, or if you want to add more, just copy and paste and write new dialogue as it comes to you. This applies to when the recycle bin is empty as well.
	}
}


//This is for the SAORI, don't touch it.
OnGarbagecanEmpty
{
	_gomi = FUNCTIONEX("gomi.dll", "-f")
}

//This is when the user chooses System Information. Don't touch any of this either. For those curious though, note that this is done using another SAORI, this time saori_cpuid.dll.
Select.ChoiceSystemInformation
{
	os_name = FUNCTIONEX("saori_cpuid.dll","os.name")
	os_version = FUNCTIONEX("saori_cpuid.dll","os.version")
	cpu_name = FUNCTIONEX("saori_cpuid.dll","cpu.name")
	cpu_clockex = FUNCTIONEX("saori_cpuid.dll","cpu.clockex")
	"/
	\0%(whwr)%(eyedles)\b[2]/
	\_q\f[bold,1]OS:\f[bold,0] %(os_name) %(os_version)\n/
	\f[bold,1]Memory Load:\f[bold,0] %(memoryload)%\n/
	\f[bold,1]Physical Memory:\f[bold,0] %(memoryavailphys) KB\n/
	\f[bold,1]CPU:\f[bold,0] %(cpu_name) %(cpu_clockex)MHz\n/
	\f[bold,1]Ukagaka:\f[bold,0] %property[baseware.version]/
	\e"
}

OnOsCheck
{
	os_name = FUNCTIONEX("saori_cpuid.dll","os.name")
	if "Windows" !_in_ os_name
	{
		programtalk = "DISABLED"
	}
}





//This is when the user chooses to check their email. The rest of this function plays out in etc.dic. You can leave this alone.
Select.MailCheck
	{
		"\![biff]\e"
	}


	
//---- OnChoiceTimeout ---------------------------------------------------------
//If the user is given a menu with choices, but doesn't pick one of the choices, it will eventually time out. At that point, they will say this dialogue. Replace with whatever you like, or leave it blank.

OnChoiceTimeout
 {
	if sleeping == 1
	{
		"\0Zzz.....\e"
		"\0Zzzz.....\e"
		"\0Zzzzz.......\e"
	}
	else
	{
		"\0\s[8]"
		"\0\s[31]"
		"\0\s[53]"
		--
		"Did you forget?"
		"Something come up?"
		"Seems you have something better to do."
		"Something more pressing then?"
		"Busy?"
		--
		"\w8\w8\w8%(eyedles)\e"
	}
	OnCalculateTrust
 }

 
//---- Secondary Character Menu ---------------------------------------------------------
//This section has to do with the menu for the second character. If you don't want your second character to have a menu, you can just delete this whole chunk.

OpenKeroMenu
 {
	"\1\s[10]\b[2]"
	"\1\s[15]\b[2]"
	"\1\s[16]\b[2]"
	
	//You've seen so far that you can set up multiple options for menu introductions. You can also set up multiple poses to match up to multiple dialogue. So for example, when you open Triangle's menu, he may randomly do any of these three poses, and randomly choose from any of the four dialogue options below, before continuing with his menu. Make a close note of the -- dashes breaking these up.
 	--
	"This is a menu introduction dialogue."
 	"This is another menu introduction dialogue."
 	"This is the third menu introduction dialogue."
 	"This is the fourth menu introduction dialogue."
 	--
	"\n\n[half]/
	\![*]\q[Update,keroupdatecheck]\n/
 	\![*]\q[Bug report,bugreport]\n\n/
	\![*]\q[Test variable,testvariable]\n\n/
	//When you are done with the test variable, aka your ghost is finished or you just don't want it showing up when you publish your ghost beta or whatevs, simply add two // slashes in front of \![*] to comment the line out. In Notepad++, it will turn green. When you want to use it again, erase the two slashes to turn it back to normal.
	\![*]\q[Nothing,keroCANCEL]\e"
}

//There are no real set things for the second character to do, really. You can mostly add whatever functions you want to them, their menu is totally free reign. In mine for example, Hunter has some games, a pseudo-FAQ for users, a bug report, an update option, and most importantly, a test variable. A lot of that is fairly complicated though, so I won't get into it here. Advanced Users can pick up Hunter and Smoker and study their menu.dic to see how I did it though, and those who read the coding page of the walkthrough and want to make their own functions can feel free to add them here. 
//For the purposes of this simple template, I'll include two options that may come in handy - an update option and a bug report option, as well as the test variable.

//If you click cancel. Replace dialogue as you want.
Select.keroCANCEL
{
	"\1\s[10]This is menu cancel dialogue.\e"
}


//This is when they select bug report. This is mostly meant to briefly give the user information about you, the creator, and how to report a problem to you. This can be handy if the person forgot where they got their ghost or how to contact you. Add whatever info you like here.
Select.bugreport
{
	//"\1\s[10]This is the bug report dialogue. \w5Report bugs to youremailaddress@emailprovider.com. \w5We were downloaded from \_a[http://www.whereveryoupostedthem.com/]some place\_a.\e"
}

//When clicked, this will automatically tell the ghost to check for updates. More detail on updates in etc.dic. You can leave this alone.
Select.keroupdatecheck
{
	"\1\s[10]\![updatebymyself]\e"
}

//--Test Variable --------------------------------------------------------
//I briefly mentioned the test variable in bootend.dic and possibly in etc.dic as well, I'm not sure. What exactly is the test variable? Basically, it's a place you can plug in anything you want to test out. While you can use Script Input to test most of your ghost's dialogue, there are some lines it just can't do, and it can't help you test choices. But the test variable can!
//You can also use this to test functions as I mentioned above by adding \![raise,function] at the end of it, although you can do this with Script Input as well. Advanced Users, the test variable is also great for testing out any new stuff you may add to your ghost, like mode settings, item values, functions, raising or lowering numbers, what have you, particularly if you have a bunch you want to change all at once.

//It is ALWAYS A GOOD IDEA to test your dialogue! If you won't check it with the test variable, at least test it with Script Input! It'll save you so much trouble!

Select.testvariable
{
	"\1\s[10]Testing!\e"
}
