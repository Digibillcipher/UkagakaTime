//---------------------------Communication--------------------------

//Alright, after many years, I've finally figured out what the deal is with this file. Basically, it handles inter-ghost communication. Some ghosts have a feature where they will speak to your ghost, usually saying "hello" or "hi" or something like that. This file mostly handles how your ghost will reply when another ghost talks to them like that. While not every ghost has that functionality, you might as well put in one line or two just in case. It won't take you any time at all.
//If you're interested in having your ghost speak to other ghosts through a menu option or something like that, I think that's a bit outside the scope of this template for now, but I'm sure I'll eventually write up how to do it on the walkthrough page in one of the coding sections.
//Some of you may be wondering about the parts for talking to the user. I can't track this down anywhere in the files, and I think it refers to a deprecated functionality to have the user teach your ghost things via the teach box. Basically just ignore it, it will probably never come up.

//As usual, refer to the walkthrough page for info on Sakurascript formatting and all that! I would definitely recommend reading the page about ghost communication as well, since that gets way more in-depth about how it works than here.


//--OnCommunicate

//OnCommunicate is called when your ghost is either targeted by another ghost for a conversation, or when your ghost is targeting another ghost for conversation. There's no need to touch any of this.
OnCommunicate
 {
	if reference0 == "user" || reference0 == "User"
 	{
		//---- answering the user
 		ReplyToUser
	}
 	else
	{
 		//---- answering another ghost
		ReplyToGhost
 	}
}

//Just ignore this.
ReplyToUser
{
	TalkToUser
}

//--ReplyToGhost

//Here is where you get to write your dialogue. This will be what your ghost will say when another ghost tries to speak to them. Most other ghosts will say something like, "Hello, ghostname" or "How are you" or "hey there". A general greeting. So think about your ghost and how they might respond to such a greeting. This functionality is again pretty rare, and while there may be a case where the other ghost just insults yours, it seems so unlikely that I wouldn't sweat it too much.

//*********************Advanced User Info*******************************
//These text boxes are meant for those who have an interest in expanding or using more complicated functions in their ghost. Also for my own reference in the future since I plan on using this template too, haha. If you have no interest in any of this and just want to keep things as simple as possible, feel free to skip these boxes.

//There are a few different ways you can refine this dialogue, depending on what ghost is speaking to yours and what they're saying. The things you can check for are reference0 and reference1, being the ghost's sakuraname (in their descript.txt) and what they're saying. For example, let's say you want to check if my Gaster ghost is speaking to yours. In that case, you'd set it up like...

/*
ReplyToGhost
{
	if reference0 == "Gaster"
	{
		res_reference0 = "Gaster" //res_reference0 would be the name of the ghost they're replying to. if the ghost they're responding to has it enabled, this will allow the other ghost to respond to this statement. if you notice that your ghost is getting stuck in a loop with the other ghost, you can erase this line and it should be fine.
		
		"\0\s[0]Hello, Gaster.\e"
	}
	else
	{
		res_reference0 = reference0 //see above, although erasing this line may make it so the res_reference0 envelope may not work. In that case, you can write around it instead.
		{
		"\0\s[0]Hello, %(res_reference0).\w8\1\s[10]Hope you're doing well.\e"
		
		"\1\s[10]Hey, %(res_reference0).\e"
		}
	}
}*/

//You can also specify it even further by looking in that ghost's dialogue with nested ifs. As an example...

/*
ReplyToGhost
{
	if reference0 == "Gaster"
	{
		res_reference0 = "Gaster" 
		
		if reference1 == "I'm a skeleton."
		{
			"\0\s[0]You sure are.\e"
		}
		else
		{
			"\0\s[0]Hello, Gaster.\e"
		}
	}
	else
	{
		res_reference0 = reference0
		{
		"\0\s[0]Hello, %(res_reference0).\w8\1\s[10]Hope you're doing well.\e"
		
		"\1\s[10]Hey, %(res_reference0).\e"
		}
	}
}*/

//As you can see, you can check reference1 for certain phrases the other ghost is saying, and reply to those phrases. You can customize your ghost's responses to another ghost's conversation in almost any way you like for any kind of custom flags or states you may have made for your ghost. It's all up to you!

//***********************************************************************

//Now, real quick, there is a chance that your ghost could get stuck in a loop of saying hi to the other ghost unless you take a few precautions. Mostly, setting a little flag to make sure your ghost won't get stuck. I'll add it in here just in case, so you won't have to worry about that.

ReplyToGhost
{
	if StartedCommu == 1
	{
		res_reference0 = reference0
		AlreadyResponded = 1
		StartedCommu = 0
		case CasperCatagory
		{
			when 0-11 //Skeleton's not in your closet
			{
				"\0\s[82]Don't worry, I don't have any skeletons in my closet.\e" //He's lying
			}
			when 12-19 //Hard to make conversation with "inanimate" objects
			{
				"\0\s[77]It spoke!\e"
			}
			when 20-23 //"I- I don't even know what that is."
			{
				"\0\s[77]Hello, there...\e"
			}
			when 24-30 //Just a little guys, just ur little buddies
			{
				"\0\s[35]Awww...\e"
			}
			when 31 //"Stars and atoms! Are you my doppelganger?"
			{
				"\0\s[51]I hope the Constant is fairing you well.\e"
			}
			others //OtherPeople Probably
			{
				"\0Sorry, I'm not good with small talk.\e"
			}
		}
	}
	elseif AlreadyResponded == 1 //checking the status of the flag
    {
		//This dialogue here is in case the other ghost doesn't have loop protection set up. As an example, let's say a ghost with no loop protection says hi to yours. The conversation would go like this.
		//Other Ghost: Hi, I'm [name].
		//Your Ghost: Hello, [name].
		//Other Ghost: Hi, I'm [name].
		//Your Ghost: Hello, [name].
			
		//And so on and so on. This dialogue down here will stop the loop, so instead it will go...
			
		//Other Ghost: Hi, I'm [name].
		//Your Ghost: Hello, [name].
		//Other Ghost: Hi, I'm [name].
		//Your Ghost: Well, it's nice to meet you. (conversation ends)
			
		//So, the dialogue you'll want here may depend on how you want your ghost to respond in the first place. This will depend on the character you are writing for! But that is the basic structure of this check, if that helps you decide what to have them say.
		
        AlreadyResponded = 0 //Resetting the flag.

        res_reference0 = "" //this will clear the value so the conversation will end
			 
		/*if reference0 == "SPAMTON!!"
		{
			"\0\s[29]Sure thing, buddy.\e"
		}
		else
		{
			"\0%(idles)Sure.\e"
		}*/
		case CasperCatagory
		{
			when 0-11 //Skeleton's not in your closet
			{
				"\0\s[82]Sure...\e"
				"\0\s[82]Alright...\e"
			}
			when 12-19 //Hard to make conversation with "inanimate" objects
			{
				"\0\s[77]It spoke again!\e"
			}
			when 20-23 //"I- I don't even know what that is."
			{
				"\0\s[77]Sure...\e"
				"\0\s[77]Alright...\e"
			}
			when 24-30 //Just a little guys, just ur little buddies
			{
				"\0\s[35]Awww...\e"
			}
			when 31 //"Stars and atoms! Are you my doppelganger?"
			{
				"\0\s[51]Sure...\e"
				"\0\s[51]Alright...\e"
			}
			others //OtherPeople Probably
			{
				"\0Sure...\e"
				"\0Alright...\e"
			}
		}
    }
    else
    {
        res_reference0 = reference0 //targeting the other ghost.
		CasperCatagory = ASEARCH(reference0,GhostsList)
        AlreadyResponded = 1 //setting a flag to say they've responded once already
		
		/*if reference0 == "SPAMTON!!"
		{
			"\0\s[29]I think I'll pass on that Spamton.\e"
		}
		else
        {
			"\0%(idles)Hello, %(res_reference0). \s[8]Hope you're doing well.\e"
        }*/
		if sleeping
		{
			"\![raise,OnGenerateSleepNoise]\w8\0%(sleepnoise)\e"
		}
		elseif sitting == 1
		{
			case CasperCatagory
			{
				when 0-11 //Skeleton's not in your closet
				{
					"\0\s[80]\w8\w8\w8Greetings, nice to see you...\e"
				}
				when 12-19 //Hard to make conversation with "inanimate" objects
				{
					"\0\s[80]It spoke!\e"
				}
				when 20-23 //"I- I don't even know what that is."
				{
					"\0\s[80]Hello, there...\e"
				}
				when 24-30 //Just a little guys, just ur little buddies
				{
					"\0\s[80]Awww...\e"
				}
				when 31 //"Stars and atoms! Are you my doppelganger?"
				{
					"\0\s[80]Stars and atoms! Are you my doppelganger?\e"
				}
				others //OtherPeople Probably
				{
					"\0\s[80]Greetings, nice to see you!\e"
				}
			}
		}
		elseif takingnotes == 1
		{
			case CasperCatagory
			{
				when 0-11 //Skeleton's not in your closet
				{
					"\0\s[50]\w8\w8\w8Greetings, nice to see you...\e"
				}
				when 12-19 //Hard to make conversation with "inanimate" objects
				{
					"\0\s[50]It spoke!\e"
				}
				when 20-23 //"I- I don't even know what that is."
				{
					"\0\s[50]Hello, there...\e"
				}
				when 24-30 //Just a little guys, just ur little buddies
				{
					"\0\s[50]Awww...\e"
				}
				when 31 //"Stars and atoms! Are you my doppelganger?"
				{
					"\0\s[50]Stars and atoms! Are you my doppelganger?\e"
				}
				others //OtherPeople Probably
				{
					"\0\s[50]Greetings, nice to see you!\e"
				}
			}
		}
		else
		{
			case CasperCatagory
			{
				when 0-11 //Skeleton's not in your closet
				{
					"\0\s[61]\w8\w8\w8\s[51]Greetings, nice to see you...\e"
				}
				when 12-19 //Hard to make conversation with "inanimate" objects
				{
					"\0\s[77]It spoke!\e"
				}
				when 20-23 //"I- I don't even know what that is."
				{
					"\0\s[77]Hello, there...\e"
				}
				when 24-30 //Just a little guys, just ur little buddies
				{
					"\0\s[35]Awww...\e"
				}
				when 31 //"Stars and atoms! Are you my doppelganger?"
				{
					"\0\s[77]Stars and atoms! Are you my doppelganger?\e"
				}
				others //OtherPeople Probably
				{
					"\0\s[51]Greetings, nice to see you!\e"
				}
			}
		}
    }
}

GhostsList : array
{
	//Skeletons
	"Gaster" //0
	"Dreamtale Sans"
	"Dusty"
	"geno"
	"Jazz"
	"Sans"
	"Papyrus"
	"Noteworthy"
	"RYS-0825"
	"sans"
	"UC Sans"
	"X. Template" //11
	
	//Got halfway through OtherPeople then realized they should be the "else"
	
	//Objects
	"Plushie" //12
	"Balloons"
	"Hydrate"
	"Needle"
	"Paper"
	"Pet Rock"
	"Radio Sakura"
	"Vending Machine" //19
	
	//UhhhhtherThings
	"one-seven-z" //20
	"Slime"
	"Squish"
	"Ukatranslator" //23
	
	//Normal-ishAnimal
	"Cat" //24
	"Neko"
	"Mudkip"
	"Snoopy"
	"Squid"
	"Tekame Hameha"
	"Warrior" //30
	
	//Wilson????
	"Wilson" //31
}

OnStartTalking
{
	_ghost = ANY(ghostexlist)
	CasperCatagory = ASEARCH(_ghost,GhostsList)
	_ghosthwnd = FUNCTIONEX("gethwnd.dll",_ghost)
	void FUNCTIONEX("window_info.dll","info",_ghosthwnd)
	_GhostInfo = valueex
	_GhostPos = _GhostInfo[2]
	res_reference0 = _ghost
	StartedCommu = 1
		
	_XLGhostCoord = TOINT(_GhostPos[0])
	_YTGhostCoord = TOINT(_GhostPos[1])
	_XRGhostCoord = TOINT(_GhostPos[2])
	_YBGhostCoord = TOINT(_GhostPos[3])
	_XGhostCoord = (_XLGhostCoord + ((_XRGhostCoord - _XLGhostCoord) / 2))
	_LookOption = 0
	if Facing == "Left"
	{
		if _XGhostCoord > XCoord
		{
			if _YBGhostCoord < YTCoord
			{
				_LookOption = 3
			}
			else
			{
				_LookOption = 1
			}
		}
		else
		{
			if _YBGhostCoord < YTCoord
			{
				_LookOption = 4
			}
			else
			{
				_LookOption = 2
			}
		}
	}
	else
	{
		if _XGhostCoord < XCoord
		{
			if _YBGhostCoord < YTCoord
			{
				_LookOption = 3
			}
			else
			{
				_LookOption = 1
			}
		}
		else
		{
			if _YBGhostCoord < YTCoord
			{
				_LookOption = 4
			}
			else
			{
				_LookOption = 2
			}
		}
	}	
	if _LookOption == 1
	{
		//"\0%(bacdles)\w8"
		"\0\s[77]WHAT!? \s[78]\w8\s[79]\w8\s[77]WHERE!?\w8\w8\w8%(bacdles)\w8\w8\w8\w8\w8\c"
		"\0\s[77]WHAT!? \s[78]\w8\s[79]\w8\s[77]HOW DID I MISS THAT!?\w8\w8\w8%(bacdles)\w8\w8\w8\w8\w8\c"
		--
		case CasperCatagory
		{
			when 0-11 //Skeleton's not in your closet
			{
				"\0\s[77]\w8\w8\w8%(bacdles)Greetings, nice to see you...\e"
			}
			when 12-19 //Hard to make conversation with "inanimate" objects
			{
				"\0\s[28]Is this some kind of joke?\e"
			}
			when 20-23 //"I- I don't even know what that is."
			{
				"\0\s[77]I'm not sure what that thing even is...\e"
			}
			when 24-30 //Just a little guys, just ur little buddies
			{
				"\0\s[35]Awww, \w8what a cute little fellow.\e"
			}
			when 31 //"Stars and atoms! Are you my doppelganger?"
			{
				"\0\s[79]Stars and atoms! Are you my doppelganger?\e"
			}
			others //OtherPeople Probably
			{
				"\0%(bacdles)Greetings, nice to see you!\e"
			}
		}
	}
	elseif _LookOption == 2
	{
		//"\0%(sidles)\w8"
		"\0\s[77]WHAT!? \s[79]\w8\s[78]\w8\s[77]WHERE!?\w8\w8\w8%(sidles)\w8\w8\w8\w8\w8\c"
		"\0\s[77]WHAT!? \s[79]\w8\s[78]\w8\s[77]HOW DID I MISS THAT!?\w8\w8\w8%(sidles)\w8\w8\w8\w8\w8\c"
		--
		case CasperCatagory
		{
			when 0-11 //Skeleton's not in your closet
			{
				"\0\s[77]\w8\w8\w8%(sidles)Greetings, nice to see you...\e"
			}
			when 12-19 //Hard to make conversation with "inanimate" objects
			{
				"\0\s[28]Is this some kind of joke?\e"
			}
			when 20-23 //"I- I don't even know what that is."
			{
				"\0\s[77]I'm not sure what that thing even is...\e"
			}
			when 24-30 //Just a little guys, just ur little buddies
			{
				"\0\s[35]Awww, \w8what a cute little fellow.\e"
			}
			when 31 //"Stars and atoms! Are you my doppelganger?"
			{
				"\0\s[78]Stars and atoms! Are you my doppelganger?\e"
			}
			others //OtherPeople Probably
			{
				"\0%(sidles)Greetings, nice to see you!\e"
			}
		}
	}
	elseif _LookOption == 3
	{
		//"\0%(upbacdles)\w8"
		"\0\s[77]WHAT!? \s[78]\w8\s[79]\w8\s[77]WHERE!?\w8\w8\w8%(upbacdles)\w8\w8\w8\w8\w8\c"
		"\0\s[77]WHAT!? \s[78]\w8\s[79]\w8\s[77]HOW DID I MISS THAT!?\w8\w8\w8%(upbacdles)\w8\w8\w8\w8\w8\c"
		--
		case CasperCatagory
		{
			when 0-11 //Skeleton's not in your closet
			{
				"\0\s[77]\w8\w8\w8%(upbacdles)Greetings, nice to see you...\e"
			}
			when 12-19 //Hard to make conversation with "inanimate" objects
			{
				"\0\s[28]Is this some kind of joke?\e"
			}
			when 20-23 //"I- I don't even know what that is."
			{
				"\0\s[77]I'm not sure what that thing even is...\e"
			}
			when 24-30 //Just a little guys, just ur little buddies
			{
				"\0\s[35]Awww, \w8what a cute little fellow.\e"
			}
			when 31 //"Stars and atoms! Are you my doppelganger?"
			{
				"\0%(upbacdles)Stars and atoms! Are you my doppelganger?\e"
			}
			others //OtherPeople Probably
			{
				"\0%(upbacdles)Greetings, nice to see you!\e"
			}
		}
	}
	elseif _LookOption == 4
	{
		//"\0%(upsidles)\w8"
		"\0\s[77]WHAT!? \s[79]\w8\s[78]\w8\s[77]WHERE!?\w8\w8\w8%(upsidles)\w8\w8\w8\w8\w8\c"
		"\0\s[77]WHAT!? \s[79]\w8\s[78]\w8\s[77]HOW DID I MISS THAT!?\w8\w8\w8%(upsidles)\w8\w8\w8\w8\w8\c"
		--
		case CasperCatagory
		{
			when 0-11 //Skeleton's not in your closet
			{
				"\0\s[77]\w8\w8\w8%(upsidles)Greetings, nice to see you...\e"
			}
			when 12-19 //Hard to make conversation with "inanimate" objects
			{
				"\0\s[28]Is this some kind of joke?\e"
			}
			when 20-23 //"I- I don't even know what that is."
			{
				"\0\s[77]I'm not sure what that thing even is...\e"
			}
			when 24-30 //Just a little guys, just ur little buddies
			{
				"\0\s[35]Awww, \w8what a cute little fellow.\e"
			}
			when 31 //"Stars and atoms! Are you my doppelganger?"
			{
				"\0%(upsidles)Stars and atoms! Are you my doppelganger?\e"
			}
			others //OtherPeople Probably
			{
				"\0%(upsidles)Greetings, nice to see you!\e"
			}
		}
	}
}

OnNeedlePoke //heheeheheheehehe
{
	_shell = reference1
	justpunched = 0
	floored = 0
	if reference0 == 0 //in case you hover over the invisible kero
	{
		if sleeping == 1
		{
			"\![raise,OnGenerateSleepNoise]\w8\0%(sleepnoise)\e"
		}
		elseif sitting == 1
		{
			pokepoke = pokepoke + 1
			
			"\0\s[80]Ow."
			"\0\s[80]Ow?"
			"\0\s[80]Ow!"
			"\0\s[80]Ouch."
			"\0\s[80]Ouch?"
			"\0\s[80]Ouch!"
			--
			" \w8\w8"
			--
			"What was that for?"
			"Don't poke me."
			"Don't do that!"
			//"Was that a needle?"
			--
			PokedWith(_shell)
			--
			"\e"
		}
		elseif takingnotes == 1
		{
			pokepoke = pokepoke + 1
			
			"\0\s[50]Ow."
			"\0\s[50]Ow?"
			"\0\s[50]Ow!"
			"\0\s[50]Ouch."
			"\0\s[50]Ouch?"
			"\0\s[50]Ouch!"
			--
			" \w8\w8"
			--
			"What was that for?"
			"Don't poke me."
			"Don't do that!"
			//"Was that a needle?"
			--
			PokedWith(_shell)
			--
			"\e\w8"
		}
		else
		{
			pokepoke = pokepoke + 1
			
			"\0\s[29]Ow."
			"\0\s[89]Ow?"
			"\0\s[7]Ow!"
			"\0\s[29]Ouch."
			"\0\s[89]Ouch?"
			"\0\s[7]Ouch!"
			--
			" \w8\w8"
			--
			"\s[29]What was that for?"
			"\s[29]Don't poke me."
			"\s[7]Don't do that!"
			//"Was that a needle?"
			--
			PokedWith(_shell)
			--
			"\e"
		}
	}
}

PokedWith
{
	_text = ""
	_shell = _argv[0]
	if _shell == "The True Needle"
	{
		_text = "\w8\w8\w8 And why was that needle so, \f[italic,true]scribbly?\f[italic,false]"
	}
	elseif _shell == "Close-Up View"
	{
		_text = "\w8\w8\w8 And why was that needle so \f[italic,true]HUGE?\f[italic,false]"
	}
	elseif _shell == "Tiny Pokey Pixels"
	{
		_text = "\w8\w8\w8 And why was that needle so, \f[italic,true]blocky?\f[italic,false]"
	}
	elseif _shell == "Syringe"
	{
		_text = "\w8\w8\w8 \f[italic,true]And what the %(insertswearheretwo) was in that syringe?\f[italic,false]"
	}
	elseif _shell == "Eggdle"
	{
		_text = "\w8\w8\w8 Wait, \w8\w8\s[77]was that a %(insertswearherethree) \f[italic,true]egg???\f[italic,false]"
	}
	lastneedleshell = _shell
	_text
}